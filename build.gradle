plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version "$jfxplugin"
    id 'com.vanniktech.maven.publish' version "$mavenPublishPlugin"
}

group 'io.github.palexdev'
version "$mfxresourcesVersion"

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = "$jdk"
    targetCompatibility = "$jdk"
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit"
}

test {
    useJUnitPlatform()
}

javafx {
    version = "$jfx"
    modules = ['javafx.controls']
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api',
                     'https://openjfx.io/javadoc/18']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJarBuild(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes(
                'Bundle-Name': project.name,
                'Bundle-Description': "Assets and resources for MaterialFX",
                'Bundle-SymbolicName': 'io.github.palexdev',
                'Export-Package': 'io.github.palexdev.mfxresources.*'
        )
    }
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

mavenPublish {
    sonatypeHost = "S01"
}